plugins {
    id "com.gradleup.shadow"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want dependencies validation to complain
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    
    // Architectury API
    modApi "dev.architectury:architectury-fabric:${rootProject.architectury_api_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive = false }

    // Version Specific Implementation
    def versionModule = ""
    def mcVerStr = rootProject.minecraft_version
    
    def useNewModule = false
    if (mcVerStr == "1.21") {
        useNewModule = false
    } else {
        def parts = mcVerStr.split("\\.")
        if (parts.length >= 3) {
             try {
                def patch = Integer.parseInt(parts[2])
                // 1.21.11 introduced KeyMapping.Category(Identifier) and MouseButtonEvent
                // Versions 1.21.5 - 1.21.9 still seem to use the 1.21.4-style API (based on build failures)
                if (patch >= 9) { 
                    useNewModule = true
                }
            } catch (NumberFormatException ignored) {}
        }
    }

    if (useNewModule) {
        versionModule = ":versions:v1_21_5"
    } else {
        versionModule = ":versions:v1_21_0"
    }

    println "Using Version Module: ${versionModule} for MC ${rootProject.minecraft_version}"

    implementation project(path: versionModule, configuration: "namedElements")
    include project(versionModule)
}

processResources {
    def versionParts = project.version_span.split("-")
    def mcDependencyRange = versionParts.size() > 1 ? ">=${versionParts[0]} <=${versionParts[1]}" : versionParts[0]

    inputs.property "version", project.version
    inputs.property "minecraft_dependency", mcDependencyRange

    filesMatching("fabric.mod.json") {
        expand "version": project.version, "minecraft_dependency": mcDependencyRange
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    injectAccessWidener = false
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
